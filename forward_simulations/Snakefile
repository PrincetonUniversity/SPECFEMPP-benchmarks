
include: "specfempp.smk"
include: "specfem2d.smk"


configfile: "config.yaml"


rule all:
    input:
        plot=expand(
            "results-{benchmark}-{machine}.png",
            benchmark=["benchmark_1", "benchmark_2", "benchmark_3"],
            machine=["cpu", "gpu"],
        ),
        csv=expand(
            "results-{benchmark}-{machine}.csv",
            benchmark=["benchmark_1", "benchmark_2", "benchmark_3"],
            machine=["cpu", "gpu"],
        ),


rule concatenate_results:
    input:
        specfempp="specfempp_workdir/{benchmark}/{machine}/results.csv",
        specfem2d="specfem2d_workdir/{benchmark}/{machine}/results.csv",
    output:
        "results-{benchmark}-{machine}.csv",
    localrule: True
    run:
        import pandas as pd

        df = pd.DataFrame()

        benchmark = wildcards.benchmark
        machine = wildcards.machine

        df = pd.concat(
            [
                df,
                pd.read_csv(input.specfempp).assign(
                    benchmark=benchmark, machine=machine, code="specfem++"
                ),
            ]
        )

        df = pd.concat(
            [
                df,
                pd.read_csv(input.specfem2d).assign(
                    benchmark=benchmark, machine=machine, code="specfem2d"
                ),
            ]
        )

        df.to_csv(output[0], index=False)


rule plot_results:
    input:
        specfempp="specfempp_workdir/{benchmark}/{machine}/results.csv",
        specfem2d="specfem2d_workdir/{benchmark}/{machine}/results.csv",
        plot_script="../scripts/plot.py",
    output:
        plot="results-{benchmark}-{machine}.png",
    localrule: True
    run:
        import sys

        sys.path.append("../scripts")

        from plot import plot
        import matplotlib.pyplot as plt

        benchmark = wildcards.benchmark
        machine = wildcards.machine
        specfem2d = f"specfem2d_workdir/{benchmark}/{machine}/results.csv"
        specfempp = f"specfempp_workdir/{benchmark}/{machine}/results.csv"

        label = config["description"][benchmark] + " (" + machine + ")"
        fig, ax = plt.subplots(figsize=(5, 4))
        plot(ax, specfem2d, specfempp, label)
        fig.text(0.5, 0.04, "Number of spectral elements", ha="center")
        fig.text(0.04, 0.5, "Simulation time (s)", va="center", rotation="vertical")
        fig.text(
            0.96,
            0.5,
            s="Speedup",
            ha="center",
            va="center",
            rotation="vertical",
            color="red",
        )
        plt.savefig(output.plot)


rule clean:
    localrule: True
    shell:
        """
        rm -rf specfempp_workdir/*/*/*/*/output.log
        rm -rf specfempp_workdir/*/*/*/*/profile.csv
        rm -rf specfem2d_workdir/*/*/*/*/output.log
        rm -rf specfem2d_workdir/*/*/*/*/profile.csv
        rm -rf specfempp_workdir/*/*/results.csv
        rm -rf specfem2d_workdir/*/*/results.csv
        rm results-*.csv
        rm results-*.png
        """
