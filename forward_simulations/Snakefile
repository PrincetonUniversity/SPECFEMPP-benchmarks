
include: "specfempp.smk"
include: "specfem2d.smk"


configfile: "config.yaml"


rule all:
    input:
        plot="results.png",
        csv="results.csv",


rule concatenate_results:
    input:
        specfempp=expand(
            "specfempp_workdir/{benchmark}/{machine}/results.csv",
            benchmark=config["benchmarks"].keys(),
            machine=config["resources"].keys(),
        ),
        specfem2d=expand(
            "specfem2d_workdir/{benchmark}/{machine}/results.csv",
            benchmark=config["benchmarks"].keys(),
            machine=config["resources"].keys(),
        ),
    output:
        "results.csv",
    localrule: True
    run:
        import pandas as pd

        df = pd.DataFrame()

        for i in range(len(input.specfempp)):
            benchmark = input.specfempp[i].split("/")[1]
            machine = input.specfempp[i].split("/")[2]
            df = pd.concat(
                [
                    df,
                    pd.read_csv(input.specfempp[i]).assign(
                        benchmark=benchmark, machine=machine, code="specfem++"
                    ),
                ]
            )

        for i in range(len(input.specfem2d)):
            benchmark = input.specfem2d[i].split("/")[1]
            machine = input.specfem2d[i].split("/")[2]
            df = pd.concat(
                [
                    df,
                    pd.read_csv(input.specfem2d[i]).assign(
                        benchmark=benchmark, machine=machine, code="specfem2d"
                    ),
                ]
            )

        df.to_csv(output[0], index=False)


rule plot_results:
    input:
        specfempp=expand(
            "specfempp_workdir/{benchmark}/{machine}/results.csv",
            benchmark=config["benchmarks"].keys(),
            machine=config["resources"].keys(),
        ),
        specfem2d=expand(
            "specfem2d_workdir/{benchmark}/{machine}/results.csv",
            benchmark=config["benchmarks"].keys(),
            machine=config["resources"].keys(),
        ),
        plot_script="../scripts/plot.py",
    output:
        plot="results.png",
    localrule: True
    run:
        import sys

        sys.path.append("../scripts")

        from plot import plot
        import matplotlib.pyplot as plt

        benchmarks = config["benchmarks"].keys()
        machines = config["resources"].keys()

        fig, ax = plt.subplots(
            len(benchmarks),
            len(machines),
            figsize=(5 * len(machines), 4 * len(benchmarks)),
        )

        # single x-axis label for all subplots
        fig.text(0.5, 0.04, "Number of spectral elements", ha="center")
        fig.text(0.04, 0.5, "Simulation time (s)", va="center", rotation="vertical")
        fig.text(
            0.96,
            0.5,
            s="Speedup",
            ha="center",
            va="center",
            rotation="vertical",
            color="red",
        )

        for i, benchmark in enumerate(benchmarks):
            for j, machine in enumerate(machines):
                specfem2d = f"specfem2d_workdir/{benchmark}/{machine}/results.csv"
                specfempp = f"specfempp_workdir/{benchmark}/{machine}/results.csv"
                label = config["description"][benchmark] + " (" + machine + ")"
                plot(ax, specfem2d, specfempp, label)

        plt.savefig(output.plot)


rule clean:
    localrule: True
    shell:
        """
        rm -rf specfempp_workdir 
        rm -rf specfem2d_workdir
        rm results.csv
        rm results.png
        """
